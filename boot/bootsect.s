# 
# BIOSç³»ç»Ÿè°ƒç”¨
# 80386åœ¨å®æ¨¡å¼ä¸‹è™½ç„¶æ˜¯16ä½çš„åœ°å€ï¼Œä½†æ˜¯ç»è¿‡æ®µæ˜ å°„åå¯ä»¥å½¢æˆ20ä½çš„åœ°å€å…±å¯»å€1MBçš„åœ°å€ç©ºé—´
# BOISä¼šåœ¨åœ°å€0å¤„å»ºç«‹1KBå­—èŠ‚çš„ä¸­æ–­å‘é‡è¡¨ï¼Œæ¯ä¸ªä¸­æ–­å‘é‡ä½¿ç”¨4ä¸ªå­—èŠ‚çš„ç©ºé—´ï¼Œ
# å‰ä¸¤ä¸ªå­—èŠ‚ä¸ºæ®µåœ°å€ï¼Œåä¸¤ä¸ªå­—èŠ‚ä¸ºåç§»åœ°å€ï¼Œå› æ­¤ä¸€å…±256ä¸ªä¸­æ–­å‘é‡
# BIOSå¯åŠ¨åä¼šå»ºç«‹åä¸­æ–­å‘é‡è¡¨ç”¨äºæä¾›ä¸€äº›åŸºæœ¬çš„åŠŸèƒ½
# æ‰€è°“BIOSè°ƒç”¨å°±æ˜¯ä½¿ç”¨BIOSçš„ä¸­æ–­åŠŸèƒ½æ¥æ‰§è¡Œä¸€äº›ç”¨æˆ·æƒ³è¦çš„æ“ä½œ
# 
# åœ¨ATæ±‡ç¼–é‡Œï¼ŒEAXè¡¨ç¤º32ä½å¯„å­˜å™¨ï¼ŒAXè¡¨ç¤º16ä½å¯„å­˜å™¨ï¼ŒAHï¼ŒALè¡¨ç¤º8ä½å¯„å­˜å™¨
#
# .code16 è¡¨ç¤ºåé¢æ˜¯16ä½çš„æ±‡ç¼–ä»£ç 
#
#
	.code16
# 
# rewrite with AT&T syntax by falcon <wuzhangjin@gmail.com> at 081012
#
# SYS_SIZE is the number of clicks (16 bytes) to be loaded.
# 0x3000 is 0x30000 bytes = 196kB, more than enough for current
# versions of linux
#
# SYSSIZEæ˜¯è¦åŠ è½½çš„èŠ‚æ•°ï¼ˆ16ä¸ªå­—èŠ‚ä¸º1èŠ‚ï¼‰0x3000*16ä¹Ÿå°±æ˜¯192KBçš„å¤§å°ï¼Œ
# å¯¹äºå½“å‰çš„å†…æ ¸æ¥è¯´å·²ç»è¶³å¤Ÿäº†
#
	.equ SYSSIZE, 0x3000
#
#	bootsect.s		(C) 1991 Linus Torvalds
#
# ç¼–è¯‘ç³»ç»Ÿç¼–è¯‘çš„é•œåƒå­˜æ”¾æ ¼å¼ä¸ºï¼š
# | 512 bootsect | 512*4 setup | system(head,kernel} |
#
# BOISä¼šå°†å¯åŠ¨è®¾å¤‡çš„å‰512å­—èŠ‚æ‹·è´è‡³å†…å­˜çš„0x7c00å¤„ï¼Œå¹¶è·³è½¬åˆ°æ­¤å¤„è¿è¡Œï¼Œ
# bootsectç¨‹åºä¸»è¦å°†è‡ªå·±ï¼ˆ512ä¸ªå­—èŠ‚ï¼‰æ¬ç§»åˆ°0x90000(576K)å¤„ï¼Œ
# ä»å¯åŠ¨è®¾å¤‡ç»§ç»­è¯»å–setupæ¨¡å—ï¼Œå­˜æ”¾åœ¨è‡ªå·±åé¢ï¼Œä¹Ÿå°±æ˜¯0x90200åœ°å€å¤„ï¼ˆ576.5Kï¼‰å¤„
# æ­¤æ—¶bootsectå’Œsetupçš„ç»“å°¾åœ°å€ä¸º0x90a00
# bootsectå’Œsetupæ¨¡å—ä¸€å…±å ç”¨2.5KBçš„ç©ºé—´ï¼Œå…¶ä¸­bootsectå ç”¨0.5KBï¼Œsetupå ç”¨2KB
#
# ä»¥ä¸Šçš„æ•°æ®è¯»å–éƒ½ä½¿ç”¨äº†BIOSè°ƒç”¨
#
#
#
# bootsect.s is loaded at 0x7c00 by the bios-startup routines, and moves
# iself out of the way to address 0x90000, and jumps there.
#
# It then loads 'setup' directly after itself (0x90200), and the system
# at 0x10000, using BIOS interrupts. 
#
# NOTE! currently system is at most 8*65536 bytes long. This should be no
# problem, even in the future. I want to keep it simple. This 512 kB
# kernel size should be enough, especially as this doesn't contain the
# buffer cache as in minix
#
# The loader has been made as simple as possible, and continuos
# read errors will result in a unbreakable loop. Reboot by hand. It
# loads pretty fast by getting whole sectors at a time whenever possible.
#
#

	.global _start, begtext, begdata, begbss, endtext, enddata, endbss
	.text
	begtext:
	.data
	begdata:
	.bss
	begbss:
	.text
#
# SETUPLEN è¡¨ç¤ºsetupæ¨¡å—å ç”¨sectorsçš„æ•°é‡ï¼Œä¸€ä¸ªsectorsä¸º512å­—èŠ‚
# BOOTSEG è¡¨ç¤ºbootsectæ¨¡å—çš„åŸå§‹è¿è¡Œåœ°å€
# INITSEG è¡¨ç¤ºbootsectæ¨¡å—æ¬ç§»åçš„è¿è¡Œåœ°å€
# SETUPSEG è¡¨ç¤ºsetupæ¨¡å—çš„è¿è¡Œåœ°å€
# SYSEG è¡¨ç¤ºsystemæ¨¡å—çš„è¿è¡Œåœ°å€ï¼Œsystemæœ€ç»ˆä¼šè¢«æ¬ç§»åˆ°0åœ°å€
# ENDSEG è¡¨ç¤ºsystemæ¨¡å—çš„ç»“æŸåœ°å€
#
	.equ SETUPLEN, 4				# nr of setup-sectors
	.equ BOOTSEG, 0x07c0			# original address of boot-sector
	.equ INITSEG, 0x9000			# we move boot here - out of the way
	.equ SETUPSEG, 0x9020			# setup starts here
	.equ SYSSEG, 0x1000				# system loaded at 0x10000 (65536).
	.equ ENDSEG, SYSSEG + SYSSIZE	# where to stop loading

# ROOT_DEV:	0x000 - same type of floppy as boot.
#		0x301 - first partition on first drive etc
# 0x301 è¡¨ç¤ºç¡¬ç›˜çš„ç¬¬ä¸€ä¸ªåˆ†åŒº
# 0x21D è¡¨ç¤ºè½¯ç›˜çš„ç¬¬ä¸€ä¸ªåˆ†åŒº
# 0x21C è¡¨çRAMDISK
#
	.equ ROOT_DEV, 0x301
#	.equ ROOT_DEV, 0x21D
#	.equ ROOT_DEV, 0x21C
#

#
# ç³»ç»Ÿå¯åŠ¨åï¼ŒBIOSä¼šå°†å¯åŠ¨è®¾å¤‡çš„å‰512å­—èŠ‚æ‹·è´è‡³0x7c00å¤„å¹¶è¿è¡Œ
# åœ¨ç¼–è¯‘bootsectæ¨¡å—ä¸­ï¼Œæˆ‘ä»¬å‘ç°äº†é“¾æ¥å‚æ•°-Ttext 0 -e _startè¡¨ç¤ºèµ·å§‹åœ°å€ä¸º0ï¼Œç¨‹åºå…¥å£ä¸º_start
# 
# è®¾ç½®DSä¸º0x07c0ï¼Œè®¾ç½®ESä¸º0x9000
# å°†SIå’ŒDIæ¸…é›¶
# movswå°†DS:SIåœ°å€å¤„çš„æ•°æ®æ‹·è´åˆ°ES:DIå¤„ï¼ŒSIå’ŒDIä¼šè‡ªåŠ¨é€’å¢ï¼Œæ‹·è´çš„æ¬¡æ•°å­˜æ”¾åœ¨CXå¯„å­˜å™¨ä¸­
# å› æ­¤ä¸‹é¢çš„ä»£ç æ„æ€æ˜¯ï¼Œ
# å°†0x7c00çš„æ•°æ®æ‹·è´è‡³0x90000(576K)å¤„, æ¯æ¬¡æ‹·è´2ä¸ªå­—èŠ‚ï¼Œå…±æ‹·è´256æ¬¡ï¼Œ512ä¸ªå­—èŠ‚
# ä¹Ÿå°±æ˜¯å°†bootsectä»0x07c00æ‹·è´åˆ°0x90000(576K)å¤„
#
# ä¸ºä»€ä¹ˆè¦æ‹·è´åˆ°0x90000(576K)å¤„,è¿™æ˜¯å› ä¸ºsystemä¼šè¢«æ‹·è´åˆ°0x10000(64K)å¤„
# è€ŒLinusåœ¨å†™è¿™ä¸ªç‰ˆæœ¬çš„Linuxçš„æ—¶å€™å‡è®¾å†…æ ¸çš„å¤§å°ä¸º512K,è¿™ä¸ªå¯ä»¥åœ¨åé¢çš„æ³¨é‡Šé‡Œçœ‹åˆ°
# 64K+512Kå°±æ˜¯576K
# é‚£ä¸ºä»€ä¹ˆsystemè¦æ‹·è´åˆ°0x10000è€Œä¸æ˜¯ç›´æ¥æ‹·è´åˆ°0x00000åœ°å€å‘¢ï¼Œ
# è¿™æ˜¯å› ä¸ºåœ¨setupæ¨¡å—ä¸­éœ€è¦ç”¨åˆ°BIOSæ‰ç”¨è·å–ä¸€äº›ç¡¬ä»¶å‚æ•°ï¼Œè€ŒBIOSå¯èƒ½å ç”¨äº†64Kçš„åœ°å€
# è¿™å°±æ˜¯ä¸ºä»€ä¹ˆåœ¨setupçš„æœ€ååˆå°†systemæ¨¡å—æ‹·è´åˆ°0x00000åœ°å€çš„åŸå› 
#
_start:
	mov	$BOOTSEG, %ax				# BOOTSEG 0x07c0
	mov	%ax, %ds					# DS = 0x07c0
	mov	$INITSEG, %ax				# INITSEC 0x9000
	mov	%ax, %es					# ES = 0x9000
	mov	$256, %cx					# CX = 256
	sub	%si, %si					# SI = 0
	sub	%di, %di					# DI = 0
	rep								# execute repeat util CX == 0, total 512 bytes
	movsw							# copy 2Bytes from DS:SI(0x07c00) to ES:DI(0x90000) 512 bytes

# 
# è·³è½¬è‡³$INITSEC:goå¤„è¿è¡Œï¼ŒINITSEGå®šä¹‰ä¸º0x9000
# å› æ­¤ä¹Ÿå°±æ˜¯è·³è½¬è‡³ä¸‹é¢çš„æ ‡å·â€œgoâ€çš„åœ°æ–¹å¼€å§‹è¿è¡Œï¼Œè¿™æ¡è¯­å¥ä¼šå°†CSè®¾ç½®ä¸ºINITSEG
#
#	
	ljmp $INITSEG, $go				# jump 0x9000:go
go:	mov	%cs, %ax					# CS = 0x9000
	mov	%ax, %ds					# DS = 0x9000
	mov	%ax, %es					# ES = 0x9000
	mov	%ax, %ss					# SS = 0x9000, put stack top at 0x9ff00
#
# æ­¤å¤„è®¾ç½®æ ˆé¡¶åœ°å€ä¸º0x9ff00
# å› ä¸ºbootsecå ç”¨512å­—èŠ‚ï¼Œsetupå ç”¨512*4ä¸ªå­—èŠ‚ï¼Œä»0x90000å¼€å§‹å­˜æ”¾bootsectå’Œsetupï¼Œæœ«å°¾åœ°å€ä¸º0x90a00
# è€Œx86çš„æ ˆä¸ºFDæ ˆï¼Œæ»¡å‡æ ˆï¼Œå› æ­¤ä»0x90a00åˆ°0x9ff00çš„ç©ºé—´éƒ½æ˜¯å¯ä»¥ç”¨ï¼Œæ ˆé¡¶æŒ‡é’ˆåˆå§‹å€¼ä¸º0x9ff00
#
	mov	$0xff00, %sp				# x86 FD stack [full decrease stack]
	                                # we will copy 4 sectors(2048) form boot device
	                                # code in 0x90000->0x90a00 and stack top 0x9ff00

# load the setup-sectors directly after the bootblock.
# Note that 'es' is already set up.
#
# ä¸‹é¢ä¸€æ®µä»£ç ä½¿ç”¨BOISç³»ç»Ÿè°ƒç”¨ä»ç¬¬äºŒä¸ªæ‰‡åŒºè¯»ï¼Œå…±è¯»å–4ä¸ªæ‰‡åŒºï¼Œ2048ä¸ªå­—èŠ‚ï¼Œæˆ‘ä»¬
# é€šè¿‡å‰é¢çš„æ³¨é‡Šå¯æ˜¯ç›´åˆ°setupæ¨¡å—ï¼Œåˆšå¥½å ç”¨4ä¸ªæ‰‡åŒºï¼Œä¸‹é¢ä»£ç çš„å·¦å³å°±æ˜¯ä»ç¬¬äºŒä¸ª
# æ‰‡åŒºå¼€å§‹è¯»å–æ•°æ®ï¼Œå­˜æ”¾åœ¨å½“å‰æ•°æ®æ®µçš„0x200å¤„ï¼Œä¹Ÿå°±æ˜¯0x90200å¤„ï¼Œè¯»å–æˆåŠŸåæŒ‘æˆ˜è‡³
# ok_load_setupå¤„å¼€å§‹è¿è¡Œï¼Œè¯»å–å¤±è´¥åç»§ç»­è¿›è¡Œå°è¯•è¯»å–
# ç›®å‰æˆ‘ä»¬åªéœ€çŸ¥é“å…¶å«ä¹‰å³å¯ï¼Œå…·ä½“å¯å‚è€ƒ<Linuxå†…æ ¸å®Œå…¨æ³¨é‡Šçš„è®²è§£>
#
load_setup:
	mov	$0x0000, %dx				# drive 0, head 0
	mov	$0x0002, %cx				# sector 2, track 0
	mov	$0x0200, %bx				# address = 512, in INITSEG
	mov $0x200+SETUPLEN, %ax		# service 2, nr of sectors, SETUPLEN is 4
	int	$0x13						# read it
	jnc	ok_load_setup				# ok - continue
	mov	$0x0000, %dx				
	mov	$0x0000, %ax				# reset the diskette
	int	$0x13
	jmp	load_setup
    
ok_load_setup:

#
# Get disk drive parameters, specifically nr of sectors/track
# è·å–å½“å‰è½¯ç›˜é©±åŠ¨çš„å‚æ•°æ”¾åœ¨sectorså¤„
#
	mov	$0x00, %dl
	mov	$0x0800, %ax				# AH=8 is get drive parameters
	int	$0x13
	mov	$0x00, %ch
	mov	%cx, %cs:sectors+0			# %cs means sectors is in %cs
	mov	$INITSEG, %ax
	mov	%ax, %es					# restore ES
#
# ä½¿ç”¨ç³»ç»Ÿè°ƒç”¨æ‰“å°ï¼šLoading system ...
# Print some inane message
#
	mov	$0x03, %ah					# read cursor pos
	xor	%bh, %bh
	int	$0x10
	
	mov	$24, %cx
	mov	$0x0007, %bx				# page 0, attribute 7 (normal)
	mov $msg1, %bp
	mov	$0x1301, %ax				# write string, move cursor
	int	$0x10

# 
# è¯»å–SYSæ¨¡å—ï¼Œå­˜æ”¾åœ¨åœ°å€0x10000ï¼ˆ64Kï¼‰å¼€å§‹çš„åœ°æ–¹ï¼Œ
# æ ¹æ®å‰é¢çš„SYSSIZEå®šä¹‰æˆ‘ä»¬çŸ¥é“ä¸€å…±è¯»å–0x3000*16ä¸ªå­—èŠ‚ä¹Ÿå°±æ˜¯192KBçš„å†…å®¹
# å¯¹äºæˆ‘ä»¬æ¥è¯´å·²ç»å¤Ÿäº†ï¼Œæˆ‘ä»¬å¯ä»¥è®¡ç®—å‡ºå½“å‰çš„æœ€å¤§åœ°å€ä¸º64 + 192 = 256KBï¼Œ
# ä¸èƒ½è¦†ç›–åˆ°bootsectå’Œsetupæ¨¡å—çš„èµ·å§‹åœ°å€
# ok, we've written the message, now
# we want to load the system (at 0x10000)
#
	mov	$SYSSEG, %ax				# AX = 0x1000
	mov	%ax, %es					# ES = 0x1000 segment of 0x010000
	call read_it					# è¯»å–systemæ¨¡å—åˆ°0x10000å¤„,çŸ¥é“å°±è¡Œï¼Œæš‚æ—¶ä¸å…³æ³¨
	call kill_motor					# å…³é—­é©±åŠ¨

#
# After that we check which root-device to use. If the device is
# defined (#= 0), nothing is done and the given device is used.
# Otherwise, either /dev/PS0 (2,28) or /dev/at0 (2,8), depending
# on the number of sectors that the BIOS reports currently.
#

	mov	%cs:root_dev+0, %ax			# è·å–root_devçš„å‚æ•°
	cmp	$0, %ax						# å’Œ0è¿›è¡Œå¯¹æ¯”
	jne	root_defined				# å¦‚æœä¸ä¸º0è¯´æ˜ï¼Œroot_devå·²ç»å®šä¹‰ï¼Œè·³è½¬åˆ°root_definedå¤„ï¼Œroot_devçš„å€¼å­˜æ”¾åœ¨axå¯„å­˜å™¨ä¸­
	mov	%cs:sectors+0, %bx			# å¦åˆ™è·å–sectorså¤„çš„æ•°æ®ï¼Œæ ¹æ®sectorsçš„æ•°æ®åˆ¤æ–­æ˜¯1.2Mè¿˜æ˜¯1.44Mçš„è½¯ç›˜
	mov	$0x0208, %ax				# /dev/ps0 - 1.2Mb
	cmp	$15, %bx					# 1.2Mçš„sectorsæ•°ç›®æ˜¯15ä¸ª
	je	root_defined				# å¦‚æœç›¸ç­‰è·³è½¬åˆ°root_definedå¤„
	mov	$0x021c, %ax				# /dev/PS0 - 1.44Mb
	cmp	$18, %bx					# 1.44Mçš„sectorsçš„æ•°ç›®æ˜¯18ä¸ª
	je	root_defined				# å¦‚æœç›¸ç­‰è·³è½¬åˆ°root_definedå¤„
undef_root:
	jmp undef_root
root_defined:
	mov	%ax, %cs:root_dev+0			# å°†axçš„å€¼å­˜æ”¾åœ¨root_devå¤„
# 
# å½“æ‰€æœ‰çš„æ¨¡å—éƒ½åŠ è½½å®Œæˆåï¼Œè·³è½¬åˆ°0x09200åœ°å€å¤„è¿è¡Œï¼Œæˆ‘ä»¬çŸ¥é“æ­¤å¤„æ˜¯setupçš„åœ°å€
# è·³è½¬åˆ° SETUPSEGçš„ 0 åç§»å¼€å§‹è¿è¡Œï¼ŒSETUPSEGä¸º0x9020ï¼Œå³åœ°å€0x90200ï¼Œ
#
# ç›®å‰ï¼Œ
# bootsectåœ¨0x90000åœ°å€å¤„å…±512å­—èŠ‚
# setupåœ¨0x90200åœ°å€å¤„å…±2KB
# systemæ¨¡å—åœ¨0x10000(64KB)åœ°å€å¤„å…±192KBå­—èŠ‚
# ä»¥ä¸Šéƒ½åœ¨å®æ¨¡å¼çš„1MBè®¿é—®ç©ºé—´å†…
#
# after that (everyting loaded), we jump to
# the setup-routine loaded directly after
# the bootblock:
#

	ljmp $SETUPSEG, $0				# setup code

# 
# This routine loads the system at address 0x10000, making sure
# no 64kB boundaries are crossed. We try to load it as fast as
# possible, loading whole tracks whenever we can.
#
# in:	es - starting address segment (normally 0x1000)
#
sread:	.word 1+ SETUPLEN			# sectors read of current track
head:	.word 0						# current head
track:	.word 0						# current track

read_it:
	mov	%es, %ax					# AX = 0x1000
	test $0x0fff, %ax				# ES must be 64KB boundary
die:	
	jne die							# es must be at 64kB boundary
	xor %bx, %bx					# bx is starting address within segment
rp_read:
	mov %es, %ax
 	cmp $ENDSEG, %ax				# have we loaded all yet?
	jb ok1_read
	ret
ok1_read:
	#seg cs
	mov	%cs:sectors+0, %ax
	sub	sread, %ax
	mov	%ax, %cx
	shl	$9, %cx
	add	%bx, %cx
	jnc ok2_read
	je 	ok2_read
	xor %ax, %ax
	sub %bx, %ax
	shr $9, %ax
ok2_read:
	call read_track
	mov %ax, %cx
	add sread, %ax
	#seg cs
	cmp %cs:sectors+0, %ax
	jne ok3_read
	mov $1, %ax
	sub head, %ax
	jne ok4_read
	incw track 
ok4_read:
	mov	%ax, head
	xor	%ax, %ax
ok3_read:
	mov	%ax, sread
	shl	$9, %cx
	add	%cx, %bx
	jnc	rp_read
	mov	%es, %ax
	add	$0x1000, %ax
	mov	%ax, %es
	xor	%bx, %bx
	jmp	rp_read

read_track:
	push %ax
	push %bx
	push %cx
	push %dx
	mov	track, %dx
	mov	sread, %cx
	inc	%cx
	mov	%dl, %ch
	mov	head, %dx
	mov	%dl, %dh
	mov	$0, %dl
	and	$0x0100, %dx
	mov	$2, %ah
	int	$0x13
	jc	bad_rt
	pop	%dx
	pop	%cx
	pop	%bx
	pop	%ax
	ret
bad_rt:	mov	$0, %ax
	mov	$0, %dx
	int	$0x13
	pop	%dx
	pop	%cx
	pop	%bx
	pop	%ax
	jmp	read_track

#
# This procedure turns off the floppy drive motor, so
# that we enter the kernel in a known state, and
# don't have to worry about it later.
# 
kill_motor:
	push %dx
	mov	$0x3f2, %dx
	mov	$0, %al
	outsb
	pop	%dx
	ret

sectors:
	.word 0

msg1:
	.byte 13,10
	.ascii "Loading system ..."
	.byte 13,10,13,10

	.org 508
root_dev:
	.word ROOT_DEV
boot_flag:
	.word 0xAA55
	
	.text
	endtext:
	.data
	enddata:
	.bss
	endbss:
	
